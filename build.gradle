group 'com.softwareverde'
version '2.0.0'

apply plugin: 'java'

sourceCompatibility = 1.7

task makeJar(type: Jar) {
    manifest {
        attributes  'Implementation-Title': 'HTTP Servlet Tomcat',
                    'Implementation-Version': version
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compileOnly 'org.apache.tomcat:tomcat-servlet-api:7.0.37'

    compile group: 'com.github.softwareverde', name: 'java-servlet', version: 'v2.0.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

tasks.withType(Test) {
    testLogging {
        events = ["passed", "skipped", "failed"]
        exceptionFormat = "full"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
